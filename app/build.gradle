apply plugin: 'com.android.application'

android {
    compileOptions {
        targetCompatibility versions.java
        sourceCompatibility versions.java
    }

    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId 'com.github.bubinimara.movies.app'
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.github.bubinimara.movies.app.AppTestRunner"

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def TMDB_API_KEY = properties.getProperty('TMDB_API_KEY')
        it.buildConfigField "String", "TMDB_API_KEY", TMDB_API_KEY
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    /* modules */
    implementation project(path: ':domain')
    implementation project(path: ':data')

    /* support */
    implementation deps.support.appcompat
    implementation deps.support.design
    implementation deps.support.cardview
    implementation deps.support.constraintlayout
    implementation deps.support.vectordrawable

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    androidTestImplementation deps.support.annotations
    testImplementation deps.junit
    testImplementation deps.support.test.monitor
    androidTestImplementation(deps.support.test.runner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(deps.support.test.espresso) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(deps.support.test.espressoContrib) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    /* view binding */
    implementation deps.butterknife
    annotationProcessor deps.butterknifecompiler

    /* rx */
    implementation deps.rxjava
    implementation deps.rxandroid

    /* retrofit */
    implementation deps.retrofit.retrofit
    implementation deps.retrofit.adapter
    implementation deps.retrofit.gson

    /* glide image loader */
    implementation(deps.glide) {
        exclude group: "com.android.support"
    }

    /* dagger di */
    implementation deps.dagger.dagger
    annotationProcessor deps.dagger.compiler

    /* leak detection */
    debugImplementation deps.leakcanary.leakcanary
    releaseImplementation deps.leakcanary.leakcanarynoop

    /* mockito */
    testImplementation deps.mockito.core
    androidTestImplementation deps.mockito.android

    /* robolectric */
    testImplementation deps.robolectric.robolectric
    testImplementation deps.robolectric.shadows
}

task androidSpoonTest(){

}
android {
    applicationVariants.all{ variant ->
        if(variant.buildType.name == "debug"){
            def apk = variant.outputs[0].outputFile
            testVariants.all { testVariant ->
                Properties properties = new Properties()
                properties.load(project.rootProject.file('local.properties').newDataInputStream())
                def sdk = properties.getProperty('sdk.dir')

                def testApk = testVariant.outputs[0].outputFile

                // Create task
                def task = tasks.create("spoon${testVariant.name.capitalize()}", JavaExec) {
                    dependsOn(testVariant.testedVariant.assemble,testVariant.outputs[0].assemble)
                    main = "-jar"
                    args = [
                            "/home/davide/git/Movies/app/spoon/spoon-runner-1.7.1-jar-with-dependencies.jar",
                            "--sdk", sdk,
                            "--apk", apk,
                            "--test-apk", testApk
                    ]
                }
                androidSpoonTest.dependsOn(task)
            }
        }
    }
}